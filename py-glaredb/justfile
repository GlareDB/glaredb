set fallback
VENV := env_var_or_default("VENV", "../.venv")
VENV_BIN := VENV / "bin"

@venv:
  python3 -c "import virtualenv" || python3 -m pip --quiet install virtualenv
  python3 -m virtualenv {{VENV}} --quiet

## Set up virtual environment and install requirements
@requirements: venv
  {{VENV_BIN}}/python -m pip --quiet install --upgrade pip
  {{VENV_BIN}}/pip --quiet install -r requirements.txt

## Compile and install py-glaredb for development
build *args: requirements
  @unset CONDA_PREFIX
  {{VENV_BIN}}/maturin develop {{args}}

test: requirements
  {{VENV_BIN}}/pytest -s

## Run autoformatting and linting
fmt: venv
  {{VENV_BIN}}/ruff .
  {{VENV_BIN}}/mypy
  cargo fmt --all

example path: requirements
  {{VENV_BIN}}/python examples/{{path}}.py
