set fallback
VENV := env_var_or_default("VENV", ".venv")
VENV_BIN := VENV / "bin"

@venv:
  python3 -c "import virtualenv" || python3 -m pip --quiet install virtualenv
  python3 -m virtualenv {{VENV}} --quiet
  {{VENV_BIN}}/python -m pip install --upgrade pip
  {{VENV_BIN}}/pip install -r requirements.txt

## Compile and install py-glaredb for development
build *args: venv
  @unset CONDA_PREFIX
  {{VENV_BIN}}/maturin develop --pip-path="{{VENV_BIN}}/pip" --extras="pandas,polars,pyarrow" {{args}}

test: venv
  {{VENV_BIN}}/pytest --rootdir={{justfile_directory()}}

## Run autoformatting and linting
fmt: venv
  {{VENV_BIN}}/ruff .
  {{VENV_BIN}}/mypy
  cargo fmt --all

example path: venv
  {{VENV_BIN}}/python examples/{{path}}.py
